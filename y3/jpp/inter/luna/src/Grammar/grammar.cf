-- programs ------------------------------------------------

entrypoints Program ;

PProgram.   Program ::= [Stmt] ;

-- FnDef.	   TopDef ::= "fn" Ident "(" [Arg] ")" ReturnTypeOpt  Block ;

-- YReturnType. ReturnTypeOpt ::=  "->" Type  ; 
-- NReturnType. ReturnTypeOpt ::=  ;

-- YTypeOpt.    TypeOpt ::= ":" Type  ; 
-- NTypeOpt.    TypeOpt ::=  ;



-- separator nonempty TopDef "" ;

AArg. 	   Arg ::= Ident ":" Type;

AArgVar.   Arg ::= "var" Ident ":" Type ;

separator  Arg "," ;

-- statements ----------------------------------------------

BBlock.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= DeclKind ;

VDecl. DeclKind ::= "var" Ident ":" Type  Item ";" ;

FDecl. DeclKind ::= "fn" Ident "(" [Arg] ")" "->" Type Block ;

Init.      Item ::= "=" Expr  ;

NoInit.    Item ::=  ; 

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For.       Stmt ::= "for" "(" Stmt ";" Expr ";" Stmt ")" Block ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Nil.       Type ::= "nil" ;

Bool.      Type ::= "bool" ;

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Fun. Type ::= "fn" "(" [Type] ")" "->" Type ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

ENil.      Expr6 ::= "nil" ;

EAny.      Expr6 ::= "any" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ; 

Not.       Expr5 ::= "not" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "and" Expr1 ;

EOr.       Expr ::= Expr1 "or" Expr ;

ELambda.   Expr ::= "fn" "(" [Arg] ")" "=>" Type  Block ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;



-- comments ------------------------------------------------


comment    "--" ;


